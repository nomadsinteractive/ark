string(TOLOWER ${CMAKE_SYSTEM_NAME} SYSTEM)
string(TOLOWER ${CMAKE_CXX_COMPILER_ID} ARK_COMPILER_ID)
string(TOLOWER ${CMAKE_CXX_COMPILER_VERSION} ARK_COMPILER_VERSION)

if(MSVC)
    add_definitions(-D_CRT_SECURE_NO_WARNINGS -D_SCL_SECURE_NO_WARNINGS -wd4251 -wd4273)
else()
    add_compile_options("-Wall")
endif()

set(ARK_GENERATED_FILE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/generated)

if(NOT ARK_TOOLS_DIR)
set(ARK_TOOLS_DIR ${ARK_SRC_DIR}/tools/cmake)
endif()

macro(ark_subdir_list result curdir)
  file(GLOB children RELATIVE ${curdir} ${curdir}/*)
  set(LOCAL_DIR_LIST "")
  foreach(child ${children})
    if(IS_DIRECTORY ${curdir}/${child})
        list(APPEND LOCAL_DIR_LIST ${child})
    endif()
  endforeach()
  set(${result} ${LOCAL_DIR_LIST})
endmacro()

macro(ark_load_platform_specific)
list(APPEND LOCAL_INCLUDE_DIRS platform/${SYSTEM})
list(APPEND LOCAL_INCLUDE_DIRS ${ARK_SRC_DIR}/platform/${SYSTEM})
include(${ARK_SRC_DIR}/platform/${SYSTEM}/public.cmake OPTIONAL)
include(platform/${SYSTEM}/platform.cmake OPTIONAL)
string(REPLACE "." ";" LOCAL_VERSION_LIST ${ARK_COMPILER_VERSION})
list(LENGTH LOCAL_VERSION_LIST LOCAL_VERSION_LIST_LEN)
if(LOCAL_VERSION_LIST_LEN GREATER 0)
    list(GET LOCAL_VERSION_LIST 0 LOCAL_MAJOR_VERSION)
    include(platform/${ARK_COMPILER_ID}-${LOCAL_MAJOR_VERSION}/platform.cmake OPTIONAL)
endif()
endmacro()

macro(ark_unset_local_variables)
get_cmake_property(_variables VARIABLES)
foreach (i ${_variables})
    string(FIND ${i} "LOCAL_" _position)
    if(_position EQUAL 0)
        unset(${i})
    endif()
endforeach()
endmacro()

function(ark_add_static_library name)
    ark_parse_dependencies(${ARGN})
    add_library(${name} STATIC ${LOCAL_SRC_LIST})
    target_compile_definitions(${name} PRIVATE ${LOCAL_COMPILE_DEFINITIONS})
    target_compile_options(${name} PUBLIC ${LOCAL_COMPILE_OPTIONS})
    target_link_libraries(${name} LINK_PUBLIC ${LOCAL_LIBS})
    target_include_directories(${name} PRIVATE ${LOCAL_INCLUDE_DIRS})
endfunction()

function(ark_add_shared_library name)
    ark_parse_dependencies(${ARGN})
    add_library(${name} SHARED ${LOCAL_SRC_LIST})
    target_compile_definitions(${name} PRIVATE ${LOCAL_COMPILE_DEFINITIONS})
    target_compile_options(${name} PRIVATE ${LOCAL_COMPILE_OPTIONS})
    target_link_libraries(${name} LINK_PRIVATE ${LOCAL_LIBS})
    target_include_directories(${name} PRIVATE ${LOCAL_INCLUDE_DIRS})
endfunction()

function(ark_add_executable name)
    ark_parse_dependencies(${ARGN})
    add_executable(${name} ${LOCAL_SRC_LIST})
    target_compile_definitions(${name} PRIVATE ${LOCAL_COMPILE_DEFINITIONS})
    target_compile_options(${name} PRIVATE ${LOCAL_COMPILE_OPTIONS})
    target_link_libraries(${name} LINK_PRIVATE ${LOCAL_LIBS})
    target_include_directories(${name} PRIVATE ${LOCAL_INCLUDE_DIRS})
endfunction()

macro(ark_enable_debug_flag)
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -DARK_FLAG_DEBUG")
endmacro()

macro(ark_setup_tools name)
foreach(i ${ARK_TOOLS_DIR})
    if(EXISTS ${i}/${name}.cmake)
        include(${i}/${name}.cmake)
        break()
    endif()
endforeach()
endmacro()

macro(ark_add_python_generated_unit UNIT_NAME WORKING_DIR SRC_PATH_LIST SCRIPT_PATH)
set(LOCAL_GENERATED_FILE_NAME ${ARK_GENERATED_FILE_DIRECTORY}/${UNIT_NAME})
if(NOT ${SRC_PATH_LIST} STREQUAL "")
    string(REPLACE " " ";" SRC_PATH_LIST ${SRC_PATH_LIST})
    foreach(i IN LISTS SRC_PATH_LIST)
    file(GLOB_RECURSE LOCAL_FILES "${WORKING_DIR}/${i}/*.h")
    list(APPEND FILE_DEPENDS ${LOCAL_FILES})
    endforeach()
endif()
if(IS_ABSOLUTE SCRIPT_PATH)
    set(LOCAL_SCRIPT_PATH ${SCRIPT_PATH})
else()
    set(LOCAL_SCRIPT_PATH ${ARK_SRC_DIR}/${SCRIPT_PATH})
endif()
add_custom_command(OUTPUT ${LOCAL_GENERATED_FILE_NAME}.h ${LOCAL_GENERATED_FILE_NAME}.cpp
    COMMAND python ${LOCAL_SCRIPT_PATH} ${ARGN}
    DEPENDS ${FILE_DEPENDS} ${LOCAL_SCRIPT_PATH}
    WORKING_DIRECTORY ${WORKING_DIR})
list(APPEND LOCAL_GENERATED_SRC_LIST ${LOCAL_GENERATED_FILE_NAME}.h)
list(APPEND LOCAL_GENERATED_SRC_LIST ${LOCAL_GENERATED_FILE_NAME}.cpp)
endmacro()

macro(ark_add_generated_file GENERATOR FILE_NAME WORKING_DIR DEPENDENCY_LIST)
set(LOCAL_GENERATED_FILE_NAME ${WORKING_DIR}/${FILE_NAME})
set(LOCAL_SCRIPT_PATH ${ARK_SRC_DIR}/${SCRIPT_PATH})
add_custom_command(OUTPUT ${LOCAL_GENERATED_FILE_NAME}
    COMMAND ${GENERATOR} ${ARGN} ${LOCAL_GENERATED_FILE_NAME}
    DEPENDS ${DEPENDENCY_LIST}
    WORKING_DIRECTORY ${WORKING_DIR})
list(APPEND LOCAL_GENERATED_SRC_LIST ${LOCAL_GENERATED_FILE_NAME})
endmacro()

function(ark_add_denpendency PATH SHARED LIB_NAME)
    set(BUILD_SHARED_LIBS ${SHARED})
    ark_ensure_dependency(${PATH})
    add_subdirectory(${PATH})
    ark_add_link_libraries(${LIB_NAME})
    set(LOCAL_LIST_VAR "${ARGN}")
    foreach(i IN LISTS LOCAL_LIST_VAR)
        list(APPEND LOCAL_INCLUDE_DIRS ${i})
    endforeach()
    ark_export(LOCAL_INCLUDE_DIRS LOCAL_LIBS LOCAL_EXPORT_TARGETS)
endfunction()

macro(ark_export)
    foreach(i ${ARGN})
        set(${i} ${${i}} PARENT_SCOPE)
    endforeach()
endmacro()

macro(ark_add_export)
    list(APPEND LOCAL_EXPORT_TARGETS ${ARGN})
endmacro()

macro(ark_add_link_libraries LIB_NAME)
    list(APPEND LOCAL_LIBS ${LIB_NAME})
    list(APPEND LOCAL_EXPORT_TARGETS ${LIB_NAME})
endmacro()

macro(ark_compile_definitions)
    list(APPEND LOCAL_COMPILE_DEFINITIONS ${ARGN})
endmacro()

macro(ark_compile_options)
    list(APPEND LOCAL_COMPILE_OPTIONS ${ARGN})
endmacro()

macro(ark_include_directories)
    list(APPEND LOCAL_INCLUDE_DIRS ${ARGN})
endmacro()

macro(ark_parse_dependencies)
    unset(LOCAL_SRC_LIST)
    foreach(i ${ARGN})
        if(TARGET ${i})
            list(APPEND LOCAL_LIBS ${i})
        elseif(IS_ABSOLUTE ${i})
            list(APPEND LOCAL_SRC_LIST ${i})
        elseif(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${i})
            list(APPEND LOCAL_SRC_LIST ${CMAKE_CURRENT_SOURCE_DIR}/${i})
        endif()
    endforeach()
endmacro()

macro(ark_test_big_endian)
    include(TestBigEndian)
    TEST_BIG_ENDIAN(IS_BIG_ENDIAN)
    if(IS_BIG_ENDIAN)
        list(APPEND LOCAL_COMPILE_DEFINITIONS -DARK_IS_BIG_ENDIAN=1)
    else()
        list(APPEND LOCAL_COMPILE_DEFINITIONS -DARK_IS_BIG_ENDIAN=0)
    endif()
endmacro()

macro(ark_ensure_dependency TARGET_DIR)
    if(ARK_DOWNLOAD_DEPENDENCIES)
        execute_process(COMMAND python ${ARK_SRC_DIR}/tools/python/dependency.py ${TARGET_DIR} ${ARGN}
                        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
    endif()
endmacro()

ark_unset_local_variables()
file(GLOB_RECURSE LOCAL_RES_LIST "*.h" "*.hpp" "*.py" "*.xml" "*.json" "*.md" "*.yaml" "*.glsl" "*.vert" "*.frag" "*.geom" "*.cmake")
ark_load_platform_specific()
